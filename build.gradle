
plugins {
    id "com.github.ben-manes.versions" version "0.39.0"
    id "net.ltgt.errorprone" version "2.0.2"
    id "java"
    id "idea"
    id "com.vanniktech.dependency.graph.generator" version "0.7.0"
    id "com.libyear.libyear-gradle-plugin" version "0.1.6"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:2.12.1")

    implementation('com.fasterxml.jackson.core:jackson-databind:2.13.2.2')
    implementation("io.reactivex.rxjava3:rxjava:3.1.4")
    implementation('io.projectreactor:reactor-core:3.4.16')
    implementation("org.threeten:threeten-extra:1.7.0")
    implementation("org.slf4j:slf4j-api:1.7.36")
    implementation("ch.qos.logback:logback-classic:1.3.0-alpha14")
    implementation("ch.qos.logback:logback-core:1.3.0-alpha14")
    implementation("org.owasp.encoder:encoder:1.2.3")
    implementation("com.hazelcast:hazelcast:5.1")
    implementation("javax.cache:cache-api:1.1.1")
    implementation('commons-codec:commons-codec:1.15')
    implementation("com.google.code.gson:gson:2.9.0")
    implementation("org.apache.commons:commons-csv:1.9.0")

    implementation("org.hibernate:hibernate-core:6.0.0.Final")
    implementation("net.ttddyy:datasource-proxy:1.7")
    implementation("org.postgresql:postgresql:42.3.3")
    implementation("org.eclipse.collections:eclipse-collections-api:11.0.0")
    implementation("org.eclipse.collections:eclipse-collections:11.0.0")

    implementation("javax.servlet:servlet-api:2.5")

    def junit4Version = "4.13.2"
    def junitVintageVersion = "5.8.2"
    def junitJupiterVersion = "5.8.2"
    def junitPlatformVersion = "1.8.2"

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")
    testImplementation("junit:junit:${junit4Version}")

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:${junitVintageVersion}") {
        because "allows JUnit 3 and JUnit 4 tests to run"
    }
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}") {
        because "allows tests to run from IDEs that bundle older version of launcher"
    }

    testImplementation("com.approvaltests:approvaltests:15.1.1")

    def mockitoVersion = "4.4.0"
    testImplementation("org.mockito:mockito-core:${mockitoVersion}")
    testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
    testImplementation('org.assertj:assertj-core:3.22.0')
    testImplementation("pl.pragmatists:JUnitParams:1.1.1")
    testImplementation('com.squareup.okhttp3:mockwebserver:4.9.3')
    testImplementation("org.awaitility:awaitility-groovy:4.2.0")
    testImplementation("com.tngtech.archunit:archunit:0.23.1")
    testImplementation("net.datafaker:datafaker:1.3.0")
    testImplementation('nl.jqno.equalsverifier:equalsverifier:3.10')

    testImplementation("org.slf4j:slf4j-simple:1.7.36") {
        because "allows a library to log during tests even when only log4j api is on path"
    }

    def testcontainersVersion = "1.16.3"
    testImplementation("org.testcontainers:testcontainers:${testcontainersVersion}")
    testImplementation("org.testcontainers:junit-jupiter:${testcontainersVersion}")
    testImplementation("org.testcontainers:postgresql:${testcontainersVersion}")
    testImplementation("net.java.dev.jna:jna:5.10.0") {
        because "testcontainers, docker and M1 chip"
    }
    testImplementation("com.larseckart:junit-tcr-extensions:0.0.1")
}

task copyDependenciesToLibs(type: Copy) {
    into "libs"
    from configurations.compileClasspath
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    //options.compilerArgs.add("-Xlint:all")
//    options.compilerArgs.add("--enable-preview")

    options.incremental = true

    options.errorprone {
        disableWarningsInGeneratedCode = true
        allErrorsAsWarnings = true
        disableAllChecks = true
    }
}


test {
    useJUnitPlatform()
    testLogging {
        events "SKIPPED", "FAILED"
        exceptionFormat "full"
    }
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
//    jvmArgs("--enable-preview")
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview", "b", "ea"].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
      }
      if (rejected) {
        selection.reject("Release candidate")
      }
    }
  }
}

libyear {
    // gw -q dependencies --configuration compileClasspath
    configurations = ['compileClasspath']
    failOnError = false
    validator = allArtifactsCombinedMustNotBeOlderThan(years(100)) // just so that build doesnt fail
}

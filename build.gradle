
plugins {
    id "com.github.ben-manes.versions" version "0.27.0"
    id 'net.ltgt.errorprone' version '1.1.1'
    id 'java'
    id 'idea'
}

repositories {
    maven() {
        url 'https://dl.bintray.com/mockito/maven/'
        content {
            includeGroup 'org.mockito'
        }
    }
    mavenCentral()
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:2.3.3")

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.0'
    implementation 'io.projectreactor:reactor-core:3.3.2.RELEASE'
    implementation 'com.google.flogger:flogger:0.4'
    runtimeOnly 'com.google.flogger:flogger-system-backend:0.4'
    implementation 'org.threeten:threeten-extra:1.5.0'
    implementation 'org.owasp.encoder:encoder:1.2.2'
    implementation 'com.hazelcast:hazelcast:3.12.4'
    implementation 'javax.cache:cache-api:1.1.1'
    implementation 'io.vavr:vavr:0.9.3'

    implementation 'org.hibernate:hibernate-core:5.4.7.Final'
    implementation 'net.ttddyy:datasource-proxy:1.5.1'
    implementation 'mysql:mysql-connector-java:8.0.18'
    implementation 'org.eclipse.collections:eclipse-collections-api:10.1.0'
    implementation 'org.eclipse.collections:eclipse-collections:10.1.0'


    def junit4Version = '4.13'
    def junitVintageVersion = '5.5.2'
    def junitJupiterVersion = '5.5.2'
    def junitPlatformVersion = '1.5.2'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")
    testImplementation("junit:junit:${junit4Version}")

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:${junitVintageVersion}") {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}") {
        because 'allows tests to run from IDEs that bundle older version of launcher'
    }

    // https://mvnrepository.com/artifact/org.netbeans/jemmy
    testImplementation group: 'org.netbeans', name: 'jemmy', version: '2.2.7.5'

    def mockitoVersion = '3.1.8'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation 'org.assertj:assertj-core:3.14.0'
    testImplementation 'com.google.truth:truth:1.0'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:1.0'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.14.0'
    testImplementation 'com.approvaltests:approvaltests:4.0.2'
    testImplementation 'org.awaitility:awaitility-groovy:4.0.1'

    def testcontainersVersion = '1.12.3'
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:mysql:${testcontainersVersion}"
}

task copyDependenciesToLibs(type: Copy) {
    into "libs"
    from configurations.compile
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add("-Xlint:all")
    options.compilerArgs.add("--enable-preview")

    options.errorprone {
        disableWarningsInGeneratedCode = true
        allErrorsAsWarnings = true
        disableAllChecks = true
    }
}
sourceCompatibility = JavaVersion.VERSION_13
targetCompatibility = JavaVersion.VERSION_13

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    jvmArgs("--enable-preview")
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

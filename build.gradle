plugins {
    id "com.github.ben-manes.versions" version "0.51.0"
    id "net.ltgt.errorprone" version "3.1.0"
    id "java"
    id "idea"
    id "application"
    id "com.vanniktech.dependency.graph.generator" version "0.8.0"
    id("com.diffplug.spotless") version "6.25.0"
//    id "com.libyear.libyear-gradle-plugin" version "0.1.6"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:2.27.1") {
        because "errorprone version is different from plugin version"
    }

    implementation('com.fasterxml.jackson.core:jackson-databind:2.17.1')
    implementation("io.reactivex.rxjava3:rxjava:3.1.8")
    implementation('io.projectreactor:reactor-core:3.6.5')
    implementation("org.threeten:threeten-extra:1.7.2")
    implementation("org.slf4j:slf4j-api:2.0.12")
    implementation("ch.qos.logback:logback-classic:1.5.6")
    implementation("ch.qos.logback:logback-core:1.5.6")
    implementation("org.owasp.encoder:encoder:1.2.3")
    implementation("com.hazelcast:hazelcast:5.3.6")
    implementation("javax.cache:cache-api:1.1.1")
    implementation('commons-codec:commons-codec:1.16.1')
    implementation("com.google.code.gson:gson:2.10.1")
    implementation("org.apache.commons:commons-csv:1.10.0")
    implementation('org.apache.commons:commons-lang3:3.14.0')

    implementation("org.hibernate:hibernate-core:6.5.0.Final")
    implementation("net.ttddyy:datasource-proxy:1.10")
    implementation("org.postgresql:postgresql:42.7.3")
    implementation("io.lettuce:lettuce-core:6.3.1.RELEASE")
    implementation 'redis.clients:jedis:5.1.2'
    implementation("org.eclipse.collections:eclipse-collections-api:11.1.0")
    implementation("org.eclipse.collections:eclipse-collections:11.1.0")
    implementation("org.apache.commons:commons-pool2:2.12.0")
    implementation 'org.dom4j:dom4j:2.1.4'

    implementation 'com.google.zxing:core:3.5.3'
    implementation 'com.google.zxing:javase:3.5.3'

    testImplementation 'com.github.mwiede:jsch:0.2.16'
    testImplementation 'com.hierynomus:sshj:0.38.0'
    implementation 'io.vavr:vavr:1.0.0-alpha-4'

    implementation("javax.servlet:servlet-api:2.5")

    // jep 320
    implementation("org.glassfish.jaxb:jaxb-runtime:4.0.5")
    implementation("org.glassfish.jaxb:jaxb-xjc:4.0.4")
    implementation("com.sun.xml.ws:jaxws-rt:4.0.2")
    implementation("javax.activation:activation:1.1.1")

    def junit4Version = "4.13.2"
    def junitVintageVersion = "5.10.2"
    def junitJupiterVersion = "5.10.2"
    def junitPlatformVersion = "1.10.2"

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")
    testImplementation("junit:junit:${junit4Version}")

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testImplementation("org.junit.platform:junit-platform-suite-engine:${junitPlatformVersion}")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:${junitVintageVersion}") {
        because "allows JUnit 3 and JUnit 4 tests to run"
    }
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}") {
        because "allows tests to run from IDEs that bundle older version of launcher"
    }

    testImplementation("com.approvaltests:approvaltests:22.3.3")

    def mockitoVersion = "5.11.0"
    testImplementation("org.mockito:mockito-core:${mockitoVersion}")
    testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
    testImplementation('org.assertj:assertj-core:3.25.3')
    testImplementation("pl.pragmatists:JUnitParams:1.1.1")
    testImplementation('com.squareup.okhttp3:mockwebserver:4.12.0')
    testImplementation("org.awaitility:awaitility-groovy:4.2.1")
    testImplementation("com.tngtech.archunit:archunit:1.3.0")
    testImplementation("net.datafaker:datafaker:2.3.0")
    testImplementation('nl.jqno.equalsverifier:equalsverifier:3.16.1')

    testImplementation("org.slf4j:slf4j-simple:2.0.13") {
        because "allows a library to log during tests even when only log4j api is on path"
    }

    def testcontainersVersion = "1.19.7"
    testImplementation("org.testcontainers:testcontainers:${testcontainersVersion}")
    testImplementation("org.testcontainers:junit-jupiter:${testcontainersVersion}")
    testImplementation("org.testcontainers:postgresql:${testcontainersVersion}")
    testImplementation("net.java.dev.jna:jna:5.14.0") {
        because "testcontainers, docker and M1 chip"
    }
    testImplementation("com.larseckart:junit-tcr-extensions:0.0.3")
    testImplementation("com.github.kirviq:dumbster:1.7.1")

    testImplementation 'org.tonicsoft.timebomb:timebomb-junit:0.0.0'
    testImplementation 'com.innoq:junit5-logging-extension:0.2.0'
}

jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

tasks.register('copyDependenciesToLibs', Copy) {
    into "libs"
    from configurations.compileClasspath
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    //options.compilerArgs.add("-Xlint:all")
    options.compilerArgs += "--enable-preview"

    options.incremental = true

    dependsOn 'spotlessApply'

    options.errorprone {
        allErrorsAsWarnings = true
        disableWarningsInGeneratedCode = true
        allErrorsAsWarnings = true
        disableAllChecks = true
    }
}

// application and main class definition
application {
    mainClassName = 'lars.Main'
}


test {
    useJUnitPlatform()
    testLogging {
        events "SKIPPED", "FAILED"
        exceptionFormat "full"
    }
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    jvmArgs("--enable-preview")
}


tasks.withType(Test).configureEach {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += "--enable-preview"
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview", "b", "ea"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}

spotless {
    java {
        googleJavaFormat()
        formatAnnotations()
    }
}
//libyear {
//    // gw -q dependencies --configuration compileClasspath
//    configurations = ['compileClasspath']
//    failOnError = false
//    validator = allArtifactsCombinedMustNotBeOlderThan(years(100)) // just so that build doesnt fail
//}

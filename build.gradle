
plugins {
    id "com.github.ben-manes.versions" version "0.27.0"
    id 'net.ltgt.errorprone' version '1.1.1'
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'java'
apply plugin: 'idea'

repositories {
    maven() {
        url 'https://dl.bintray.com/mockito/maven/'
        content {
            includeGroup 'org.mockito'
        }
    }
    mavenCentral()
}
dependencies {
    errorprone("com.google.errorprone:error_prone_core:2.3.3")

    compile 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    compile 'com.google.code.gson:gson:2.8.6'
    compile 'com.squareup.moshi:moshi:1.8.0'
    compile 'io.reactivex.rxjava2:rxjava:2.2.13'
    compile 'com.google.flogger:flogger:0.4'
    runtime 'com.google.flogger:flogger-system-backend:0.4'
    compile 'org.threeten:threeten-extra:1.5.0'
    compile 'org.owasp.encoder:encoder:1.2.2'

    compile 'org.hibernate:hibernate-core:5.4.7.Final'
    compile 'net.ttddyy:datasource-proxy:1.5.1'
    compile 'mysql:mysql-connector-java:8.0.18'


    def junit4Version = '4.12'
    def junitVintageVersion = '5.5.2'
    def junitJupiterVersion = '5.5.2'
    def junitPlatformVersion = '1.5.2'

    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testCompile("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")
    testCompile("junit:junit:${junit4Version}")

    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testRuntime("org.junit.vintage:junit-vintage-engine:${junitVintageVersion}") {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }
    testRuntime("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}") {
        because 'allows tests to run from IDEs that bundle older version of launcher'
    }

    // https://mvnrepository.com/artifact/org.netbeans/jemmy
    testCompile group: 'org.netbeans', name: 'jemmy', version: '2.2.7.5'

    def mockitoVersion = '3.1.1'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation 'org.assertj:assertj-core:3.13.2'
    testImplementation 'com.google.truth:truth:1.0'
    testImplementation 'com.google.truth.extensions:truth-java8-extension:1.0'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.14.0'
    testImplementation 'com.approvaltests:approvaltests:4.0.2'
    testImplementation 'org.awaitility:awaitility-groovy:4.0.1'

    def testcontainersVersion = '1.12.2'
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:mysql:${testcontainersVersion}"

}


task copyDependenciesToLibs(type: Copy) {
    into "libs"
    from configurations.compile
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" //<< "-Werror"

    options.errorprone {
        disableWarningsInGeneratedCode = true
        disableAllChecks = true
    }
}
sourceCompatibility = '11'
targetCompatibility = '11'

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
    }
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
